#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newNetworkRouteRef ${nuklei:newReferenceId()} # external scope

property newTimestamp1 ${kafka:timestamp()}
property newTimestamp2 ${kafka:timestamp()}

property networkAccept "nukleus://kafka/streams/source"
property networkAcceptWindow 8192

accept ${networkAccept}
  option nukleus:route  ${newNetworkRouteRef}
  option nukleus:window ${networkAcceptWindow}
  option nukleus:transmission "duplex"
  option nukleus:byteorder "network"

# Metadata stream
accepted
connected

read 21         # Size int32
read 0x03s      # ApiKey int16 (Metadata)
read 0x05s      # ApiVersion int16
read (int:metadataRequestId)
read -1s        # ClientId string (null)
read 1          # [TopicName] array length
  read 4s "test"
read [0x00]     # allow_auto_topic_creation (boolean)

write 85        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # brokers array length
  write 1       # broker id
  write 7s "broker1"
  write 9093    # port int32
  write -1s     # rack string (null)
write 9s "cluster 1"
write 1         # controller broker id
write 1         # topic array length
  write 0s      # error code
  write 4s "test"
  write byte 0x00           # is_internal
  write 1       # partition array length
    write 0s    # error code
    write 0     # partition
    write 1     # leader
    write 0     # replicas array (empty)
    write -1    # isr array (null)
    write 0     # offline replicas array (empty)

read 41         # Size int32
read 32s        # ApiKey int16 (DescribeConfigs)
read 0s         # ApiVersion int16
read (int:metadataRequestId) # CorrelationId int32
read -1s        # ClientId string (null)
read 1          # [Resources] array length
read [0x02]     # resource type int8 (topic) 
read 4s "test"  # topic name
read 1          # config_names count
read 14s "cleanup.policy"

write 54        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # resources count
write 0s        # error code
write -1s       # error message
write [0x02]    # resource type
write 4s "test" # topic name
write 1         # config entries count
write 14s "cleanup.policy"  # config name
write 6s "delete"           # config  value
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean

# List offsets stream
accepted
read nukleus:begin.ext ${tcp:beginExtRemoteHost("broker1", 9093)}
connected

read 41         # Size int32
read 2s         # ApiKey (ListOffsets)
read 2s         # ApiVersion 
read (int:listOffsetsRequestId) # CorrelationId 
read -1s        # ClientId string (null)
read -1         # replicaId
read [0x00]     # isolation level (0=READ_UNCOMMITTED)
read 1          # topic count 
read 4s "test"  # topic name
read 1          # partition count
read 0          # partition
read -2L        # timestamp (earliest available offset)

write 44        # Size int32
write ${listOffsetsRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # topic count
write 4s "test" # topic name 
write 1         # partition count
write 0         # partition
write 0s        # error code
write -1L       # timestamp (from a real example)
write 0L        # offset

# Live Fetch stream
accepted
read nukleus:begin.ext ${tcp:beginExtRemoteHost("broker1", 9093)}
connected

read 65         # Size
read 0x01s      # Fetch
read 0x05s
read (int:requestId1)
read -1s
read -1
read [0..4]
read 0x01
read [0..4]
read [0x00]
read 0x01
read 0x04s "test"
read 0x01
read 0x00       # Partition
read 0x00L      # offset
read -1L
read [0..4]

write 140       # Size
write ${requestId1}
write 0
write 1         # Number of topics
write 0x04s "test"
write 1         # Number of partitions
write 0         # Partition
write 0x00s     # partition error code
write 0x01L     # high_watermark
write -1L       # last_stable_offset
write 0L        # log_start_offset
write -1        # aborted_transactions (null)
write 80        # length of record batch
write 0x00L     # First offset
write 68        # length
write 0         # partition leader epoque
write [0x02]    # magic
write 0x4e8723aa            # CRC32
write 0x00s     # attributes
write 0         # last offset delta
write ${newTimestamp1}      # first timestamp
write ${newTimestamp1}      # maximum timestamp
write -1L       # producer ID
write -1s       # producer epoque
write -1        # first sequence
write 1         # number of records
write ${kafka:varint(18)}   # Record length
write [0x00]    # attributes
write ${kafka:varint(0)}    # timestamp delta
write ${kafka:varint(0)}    # offset delta
write ${kafka:varint(-1)}   # key length
write ${kafka:varint(12)}   # value length
write "Hello, world"
write [0x00]

read 0x41
read 0x01s
read 0x05s
read (int:requestId3)
read -1s
read -1
read [0..4]
read 0x01
read [0..4]
read [0x00]
read 0x01
read 0x04s "test"
read 0x01
read 0x00       # partition
read 0x01L      # offset
read -1L
read [0..4]

write await SERVER_DELIVER_LIVE_RESPONSE_TWO

write 140       # Size
write ${requestId3}
write 0
write 1         # Number of topics
write 0x04s "test"
write 1         # Number of partitions
write 0         # Partition
write 0x00s     # partition error code
write 0x02L     # high_watermark
write -1L       # last_stable_offset
write 0L        # log_start_offset
write -1        # aborted_transactions (null)
write 80        # length of record batch
write 0x01L     # First offset
write 68        # length
write 0         # partition leader epoque
write [0x02]    # magic
write 0x4e8723aa            # CRC32
write 0x00s     # attributes
write 0         # last offset delta
write ${newTimestamp2}      # first timestamp
write ${newTimestamp2}      # maximum timestamp
write -1L       # producer ID
write -1s       # producer epoque
write -1        # first sequence
write 1         # number of records
write ${kafka:varint(18)}   # Record length
write [0x00]    # attributes
write ${kafka:varint(0)}    # timestamp delta
write ${kafka:varint(0)}    # offset delta
write ${kafka:varint(-1)}   # key length
write ${kafka:varint(12)}   # value length
write "Hello, again"
write [0x00]

# Historical Fetch stream
accepted
read nukleus:begin.ext ${tcp:beginExtRemoteHost("broker1", 9093)}
connected

read 65         # Size
read 0x01s      # Fetch
read 0x05s
read (int:requestId2)
read -1s
read -1
read [0..4]
read 0x01
read [0..4]
read [0x00]
read 0x01
read 0x04s "test"
read 0x01
read 0x00       # Partition
read 0x00L      # offset
read -1L
read [0..4]

write 159       # Size
write ${requestId2}
write 0
write 1         # Number of topics
write 0x04s "test"
write 1         # Number of partitions
write 0         # partition
write 0x00s     # partition error code
write 2L        # high_watermark
write -1L
write 0L
write -1
write 99        # length of record batch
write 0x00L     # first offset
write 87        # length
write 0         # partition leader epoque
write [0x02]    # magic
write 0x4e8723aa            # CRC32
write 0s        # attributes
write 1         # last offset delta
write ${newTimestamp1}      # first timestamp
write ${newTimestamp2}      # last timestamp
write -1L       # producer ID
write -1s       # producer epoque
write -1        # first sequence
write 2         # number of records
write ${kafka:varint(18)}   # Record length
write [0x00]    # attributes
write ${kafka:varint(0)}    # timestamp delta
write ${kafka:varint(0)}    # offset delta
write ${kafka:varint(-1)}   # key length
write ${kafka:varint(12)}   # value length
write "Hello, world"
write [0x00]    # headers array length

write ${kafka:varint(18)}   # Record length
write [0x00]
write ${kafka:varint(newTimestamp2 - newTimestamp1)} # timestamp delta
write ${kafka:varint(1)}    # offset delta
write ${kafka:varint(-1)}   # key length
write ${kafka:varint(12)}   # value length
write "Hello, again"
write [0x00]    # headers array length
