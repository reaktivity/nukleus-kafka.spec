#
# Copyright 2016-2020 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property networkAccept "nukleus://streams/kafka#0"
property networkAcceptWindow 8192

property deltaMillis 0L
property newTimestamp ${kafka:timestamp() + deltaMillis}

accept ${networkAccept}
  option nukleus:window ${networkAcceptWindow}
  option nukleus:padding 512
  option nukleus:transmission "duplex"
  option nukleus:byteorder "network"

accepted

connected

read 21                                 # size
     3s                                 # metadata
     5s                                 # v5
     (int:requestId)
     -1s                                # no client id
     1                                  # topics
       4s "test"                        #   "test" topic
     [0x00]                             # allow_auto_topic_creation

write 97                                # size
      ${requestId}
      0
      1                                 # brokers
        0xb1                                # broker id
        19s "broker1.example.com"           # host name
        9092                                # port
        -1s                                 # no rack
      9s "cluster 1"                    # cluster id
      1                                 # controller id
      1                                 # topics
        0s                                # no error
        4s "test"                         #   "test" topic
        [0x00]                            # not internal
        1                                 # partitions
          0s                                # no error
          0                                 # partition
          0xb1                              # leader
          0                                 # no replicas
          -1                                # no in-sync replicas
          0                                 # offline replicas

accepted

read nukleus:begin.ext ${tcp:beginEx()
                            .typeId(nukleus:id("tcp"))
                            .localAddress("0.0.0.0")
                            .localPort(0)
                            .remoteHost("broker1.example.com")
                            .remotePort(9092)
                            .build()}

connected

read 102512
     0s
     3s
     (int:requestId)
     -1s
     -1s
     [0..2]
     [0..4]
     1
     4s "test"
     1
     0
     102472                             # record set size
     0L                                 # first offset
     102460                             # length
     -1
     [0x02]
     [0..4]
     0s
     0                                  # last offset delta
     (long:timestamp)                   # first timestamp
     ${timestamp}                       # last timestamp
     -1L
     -1s
     -1
     1                                  # records
     ${kafka:varint(102407)}
     [0x00]
     ${kafka:varint(0)}
     ${kafka:varint(0)}
     ${kafka:varint(-1)}                # key
     ${kafka:varint(102400)}             # value
     [0..102400]
     ${kafka:varint(0)}                 # headers

write 44
      ${requestId}
      1                                 # topics
      4s "test"
      1                                 # partitions
      0                                 # partition 0
      0s                                # no error
      20L                               # base offset
      0L                                # log append time
      0                                 # throttle
