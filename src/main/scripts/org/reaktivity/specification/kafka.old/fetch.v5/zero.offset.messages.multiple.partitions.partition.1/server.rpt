#
# Copyright 2016-2019 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newTimestamp ${kafka:timestamp()}

property networkAccept "nukleus://streams/kafka#0"
property networkAcceptWindow 8192

accept ${networkAccept}
  option nukleus:window ${networkAcceptWindow}
  option nukleus:transmission "duplex"
  option nukleus:byteorder "network"

# Metadata connection
accepted
connected

read 21         # Size int32
read 3s      # ApiKey int16 (Metadata)
read 5s      # ApiVersion int16
read (int:metadataRequestId) # CorrelationId int32
read -1s        # ClientId string (null)
read 1          # [TopicName] array length
  read 4s "test"
read [0x00]     # allow_auto_topic_creation (boolean)

write 107       # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # brokers array length
  write 1       # broker id
  write 7s "broker1"
  write 9093    # port int32
  write -1s     # rack string (null)
write 9s "cluster 1"
write 1         # controller broker id
write 1         # topic array length
  write 0s      # error code
  write 4s "test"
  write byte 0           # is_internal
  write 2       # partition array length
    write 0s    # error code
    write 0     # partition
    write 1     # leader
    write 0     # replicas array (empty)
    write -1    # isr array (null)
    write 0     # offline replicas array (empty)

    write 0s    # error code
    write 1     # partition
    write 1     # leader
    write 0     # replicas array (empty)
    write -1    # isr array (null)
    write 0     # offline replicas array (empty)

read 62         # Size int32
read 32s        # ApiKey int16 (DescribeConfigs)
read 0s         # ApiVersion int16
read (int:metadataRequestId) # CorrelationId int32
read -1s        # ClientId string (null)
read 1          # [Resources] array length
read [0x02]     # resource type int8 (topic) 
read 4s "test"  # topic name
read 2          # config_names count
read 14s "cleanup.policy"
read 19s "delete.retention.ms"

write 88        # Size int32
write ${metadataRequestId}  # CorrelationId int32
write 0         # throttle_time_ms int32
write 1         # resources count
write 0s        # error code
write -1s       # error message
write [0x02]    # resource type
write 4s "test" # topic name
write 2         # config entries count
write 14s "cleanup.policy"  # config name
write 6s "delete"           # config  value
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean
write 19s "delete.retention.ms"
write 8s "86400000"
write [0x00]    # read_only boolean
write [0x01]    # is_default boolean
write [0x00]    # is_sensitive boolean

# Fetch stream
accepted
read nukleus:begin.ext ${tcp:beginEx()
                            .typeId(nukleus:id("tcp"))
                            .localAddress("0.0.0.0")
                            .localPort(0)
                            .remoteHost("broker1")
                            .remotePort(9093)
                            .build()}
connected

read 89
read 1s
read 5s
read (int:requestId)
read -1s        # client id
read -1 
read [0..4]
read 1
read [0..4]
read [0x00]
read 1
read 4s "test"
read 2          # Number of partitions
read 0          # Partition
read 0L
read -1L
read [0..4]
read 1          # Partition
read 0L
read -1L
read [0..4]

write 178
write ${requestId}
write 0
write 1
write 4s "test"
write 2         # Number of partition responses
    write 0     # Partition
    write 0s
    write 1L # high_watermark
    write -1L   # last_stable_offset
    write 0L    # log_start_offset
    write -1    # aborted_transactions (null)
    write 0  # Length of the record batch (no messages)
  
    write 1     # Partition
    write 0s
    write 1L # high_watermark
    write -1L   # last_stable_offset
    write 0L    # log_start_offset
    write -1    # aborted_transactions (null)
    write 0x50  # Length of the record batch
                # Start of RecordBatch
    write 0L # First offset 
    write 0x44  # Length
    write 0  # Partition leader epoque
    write [0x02]            # Magic
    write 0x4e8723aa        # CRC32
    write 0s # attributes
    write 0     # last offset delta
    write ${newTimestamp}
    write ${newTimestamp}
    write -1L   # producer ID
    write -1s   # producer epoque
    write -1    # first sequence
    write 1     # Number of records
    write ${kafka:varint(18)} # Record length
    write [0x00]            # attributes
    write ${kafka:varint(0)} # timestamp delta
    write ${kafka:varint(0)} # offset delta
    write ${kafka:varint(-1)} # key length
    write ${kafka:varint(12)} # value length
    write "Hello, world"    # value
    write ${kafka:varint(0)} # headers array length

# Second live fetch request
read 89
read 1s
read 5s
read (int:requestId)
read -1s        # client id
read -1 
read [0..4]
read 1
read [0..4]
read [0x00]
read 1
read 4s "test"
read 2          # Number of partitions
read 0          # Partition
read 0L
read -1L
read [0..4]
read 1          # Partition
read 1L         # offset
read -1L
read [0..4]

write 178
write ${requestId}
write 0
write 1
write 4s "test"
write 2         # Number of partition responses
    write 0     # Partition
    write 0s
    write 0L # high_watermark
    write -1L   # last_stable_offset
    write 0L    # log_start_offset
    write -1    # aborted_transactions (null)
    write 0  # Length of the record batch (no messages)
  
    write 1     # Partition
    write 0s
    write 2L # high_watermark
    write -1L   # last_stable_offset
    write 1L    # log_start_offset
    write -1    # aborted_transactions (null)
    write 0x50  # Length of the record batch
                # Start of RecordBatch
    write 1L # First offset 
    write 0x44  # Length
    write 0  # Partition leader epoque
    write [0x02]            # Magic
    write 0x4e8723aa        # CRC32
    write 0s # attributes
    write 0     # last offset delta
    write ${newTimestamp}
    write ${newTimestamp}
    write -1L   # producer ID
    write -1s   # producer epoque
    write -1    # first sequence
    write 1     # Number of records
    write ${kafka:varint(18)} # Record length
    write [0x00]            # attributes
    write ${kafka:varint(0)} # timestamp delta
    write ${kafka:varint(0)} # offset delta
    write ${kafka:varint(-1)} # key length
    write ${kafka:varint(12)} # value length
    write "Hello, again"    # value
    write ${kafka:varint(0)} # headers array length


# historical fetch connection
accepted
read nukleus:begin.ext ${tcp:beginEx()
                            .typeId(nukleus:id("tcp"))
                            .localAddress("0.0.0.0")
                            .localPort(0)
                            .remoteHost("broker1")
                            .remotePort(9093)
                            .build()}
connected
